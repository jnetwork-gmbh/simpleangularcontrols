echo off
CHCP 1252
SETLOCAL
Echo ******** WAMS BUILD PROCESS
Echo.
Echo ** Build Angular Controls Library


set mode=%1

if "%mode%"=="" (
    goto help
)

if not %mode%==major (
    if not %mode%==minor (
        if not %mode%==patch (
            if not %mode%==none (
                echo Invalid Parameter
                echo.
                goto help
            )   
        )
    )
)

echo Build Mode %mode%
echo.
echo Build Angular Components

cd ..\exanic.ch.controls.angular\application

call :prebuild
IF ERRORLEVEL 1 GOTO :END
   
echo Build erfolgreich

if not %mode%==none (
    call :setversion
) else (
    echo Version wird nicht angepasst: Mode none
)

call :build

call :pack

call :push

cd ..\..\buildscripts

goto end


:prebuild
    
    call :build
    
    echo.
    set /p result=Build erfolgreich? y/n: 
    echo. 
    
    if not %result%==y (
        echo Cancel Build process
        cd ..\..\buildscripts
        exit /b 1
    )
    
    goto :EOF

:build

    echo Begin Build Angular
    
    call ng build exanic-controls-common
    call ng build exanic-controls-bootstrap3
    call ng build exanic-controls-bootstrap4
    
    echo End Build Angular
    
    goto :EOF

:setversion

    echo Increase Version with Mode %mode%

    cd projects\exanic-controls-common
    call npm version %mode%
    cd ..\..
    
    cd projects\exanic-controls-bootstrap3
    call npm version %mode%
    cd ..\..
    
    cd projects\exanic-controls-bootstrap4
    call npm version %mode%
    cd ..\..

    goto :EOF
   
:pack
    echo Pack NPM Packages

    echo.
    echo **********************************************************************************************************
    echo !!! In package.json im Dist Folder für Bootstrap 3/4 die Version für die Dependencies manuell anpassen !!!
    echo **********************************************************************************************************
    echo.
    set /p result=Paket erzeugen? y/n: 
    echo. 
    
    if not %result%==y (
        echo Cancel Pack process
        exit /b 1
    )
    
    cd dist\exanic-controls-common
    call npm pack
    cd ..\..
    
    cd dist\exanic-controls-bootstrap3
    call npm pack
    cd ..\..
    
    cd dist\exanic-controls-bootstrap4
    call npm pack
    cd ..\..

    echo Pack NPM Packages done
    echo.
    echo.
    
    goto :EOF   

:push

    echo.
    echo ***************************************************************************************************
    echo !!! Sicherstellen das Registry @exanic eingetragen ist und das Benutzer Login für NPM aktiv ist !!!
    echo ***************************************************************************************************
    echo.
    set /p result=Paket auf Server veröffentlichen? y/n: 
    echo. 
    
    if not %result%==y (
        echo Cancel Publish process
        exit /b 1
    )

    echo Publish NPM Packages
    
    cd dist\exanic-controls-common
    call npm publish
    cd ..\..
    
    cd dist\exanic-controls-bootstrap3
    call npm publish
    cd ..\..
    
    cd dist\exanic-controls-bootstrap4
    call npm publish
    cd ..\..
    
    echo Publish NPM Packages done
    echo.
    echo.
    
    goto :EOF
    
:help
    echo Als Parameter muss der Release Mode mitgegeben werden.
    echo.
    echo Parameter:
    echo major
    echo minor
    echo patch
    echo none
    echo.
    echo.
    echo Script abgebrochen
    echo.
    goto end
    
:end
